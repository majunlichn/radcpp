#version 450 core

#extension GL_GOOGLE_include_directive : require

#include "Common/DataType.h"

layout(push_constant, std430) uniform PushConstants
{
    uint g_inputIndexOffset;
    uint g_outputIndexOffset;
};

#ifndef PARAM_TYPE
    #if DATA_TYPE_IS_FLOATING_POINT
        #if ELEMENT_SIZE <= 4
            #define PARAM_TYPE f32vec4
        #elif DATA_TYPE_ID == DataTypeFloat64
            #define PARAM_TYPE f64vec4
        #endif
    #elif DATA_TYPE_IS_SIGNED_INTEGRAL
        #if ELEMENT_SIZE <= 4
            #define PARAM_TYPE i32vec4
        #else
            #define PARAM_TYPE i64vec4
        #endif
    #elif DATA_TYPE_IS_UNSIGNED_INTEGRAL
        #if ELEMENT_SIZE <= 4
            #define PARAM_TYPE u32vec4
        #else
            #define PARAM_TYPE u64vec4
        #endif
    #endif // DATA_TYPE_IS_*
#endif // PARAM_TYPE

layout(set = 0, binding = 0) uniform Uniforms
{
    uvec4 g_sizes;
    uvec4 g_inputStrides;
    uvec4 g_outputStrides;
    uvec4 g_padding; // Unused, just for alignment
    PARAM_TYPE g_params;
};

layout(set = 0, binding = 1) readonly buffer InputTensor { DATA_TYPE g_inputTensor[]; };
layout(set = 0, binding = 2) buffer OutputTensor { DATA_TYPE g_outputTensor[]; };

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

#if defined(AddScalar)
COMPUTE_TYPE AddScalar(COMPUTE_TYPE x)
{
    return x + COMPUTE_TYPE(g_params[0]);
}
#endif

#if defined(SubtractScalar)
COMPUTE_TYPE SubtractScalar(COMPUTE_TYPE x)
{
    return x - COMPUTE_TYPE(g_params[0]);
}
#endif

#if defined(MultiplyScalar)
COMPUTE_TYPE MultiplyScalar(COMPUTE_TYPE x)
{
    return x * COMPUTE_TYPE(g_params[0]);
}
#endif

#if defined(DivideScalar)
COMPUTE_TYPE DivideScalar(COMPUTE_TYPE x)
{
    return x / COMPUTE_TYPE(g_params[0]);
}
#endif

#if defined(RemainderScalar)
COMPUTE_TYPE RemainderScalar(COMPUTE_TYPE x)
{
    const COMPUTE_TYPE a = x;
    const COMPUTE_TYPE b = COMPUTE_TYPE(g_params[0]);
#if DATA_TYPE_IS_FLOATING_POINT
    return mod(a, b);
#else
    return COMPUTE_TYPE(int(a) % int(b));
#endif
}
#endif

#if defined(BitwiseAndScalar)
COMPUTE_TYPE BitwiseAndScalar(COMPUTE_TYPE x)
{
    return x & COMPUTE_TYPE(g_params[0]);
}
#endif

#if defined(BitwiseOrScalar)
COMPUTE_TYPE BitwiseOrScalar(COMPUTE_TYPE x)
{
    return x | COMPUTE_TYPE(g_params[0]);
}
#endif

#if defined(BitwiseXorScalar)
COMPUTE_TYPE BitwiseXorScalar(COMPUTE_TYPE x)
{
    return x ^ COMPUTE_TYPE(g_params[0]);
}
#endif

void main()
{
    const uint SizeN = g_sizes[0];
    const uint SizeC = g_sizes[1];
    const uint SizeH = g_sizes[2];
    const uint SizeW = g_sizes[3];
    const uint n = gl_GlobalInvocationID.z;
    const uint h = gl_GlobalInvocationID.y;
    const uint w = gl_GlobalInvocationID.x;
    if ((w >= SizeW) || (h >= SizeH))
    {
        return;
    }
    for (uint c = 0; c < SizeC; ++c)
    {
        uint inputIndex = g_inputIndexOffset + n * g_inputStrides[0] + c * g_inputStrides[1] +
                            h * g_inputStrides[2] + w * g_inputStrides[3];
        uint outputIndex = g_outputIndexOffset + n * g_outputStrides[0] + c * g_outputStrides[1] +
                            h * g_outputStrides[2] + w * g_outputStrides[3];
        COMPUTE_TYPE x = g_inputTensor[inputIndex];
        g_outputTensor[outputIndex] = DATA_TYPE(ElementOp(x));
    }
}
