#version 450 core

#extension GL_GOOGLE_include_directive : require

#include "Common/DataType.h"

layout(push_constant, std430) uniform PushConstants
{
    uint g_inputIndexOffset;
    uint g_outputIndexOffset;
};

#if ((DATA_TYPE_ID == DataTypeFloat16) || (DATA_TYPE_ID == DataTypeFloat32))
    struct Params { float32_t s0; float32_t s1; float32_t s2; float32_t s3; };
#elif (DATA_TYPE_ID == DataTypeFloat64)
    struct Params { float64_t s0; float64_t s1; float64_t s2; float64_t s3; };
#elif ((DATA_TYPE_ID == DataTypeSint8) || (DATA_TYPE_ID == DataTypeSint16) || (DATA_TYPE_ID == DataTypeSint32))
    struct Params { int32_t s0; int32_t s1; int32_t s2; int32_t s3; };
#elif (DATA_TYPE_ID == DataTypeSint64)
    struct Params { int64_t s0; int64_t s1; int64_t s2; int64_t s3; };
#elif ((DATA_TYPE_ID == DataTypeUint8) || (DATA_TYPE_ID == DataTypeUint16) || (DATA_TYPE_ID == DataTypeUint32))
    struct Params { uint32_t s0; uint32_t s1; uint32_t s2; uint32_t s3; };
#elif (DATA_TYPE_ID == DataTypeUint64)
    struct Params { uint64_t s0; uint64_t s1; uint64_t s2; uint64_t s3; };
#elif (DATA_TYPE_ID == DataTypeBool)
    struct Params { uint32_t s0; uint32_t s1; uint32_t s2; uint32_t s3; };
#elif DATA_TYPE_IS_COMPLEX
    struct Params { COMPLEX s0; COMPLEX s1; COMPLEX s2; COMPLEX s3; };
#endif

layout(set = 0, binding = 0) uniform Uniforms
{
    uvec4 g_sizes;
    uvec4 g_inputStrides;
    uvec4 g_outputStrides;
    Params g_params;
};

layout(set = 0, binding = 1) readonly buffer InputTensor { DATA_TYPE g_inputTensor[]; };
layout(set = 0, binding = 2) buffer OutputTensor { DATA_TYPE g_outputTensor[]; };

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

#if defined(AddScalar)
#if DATA_TYPE_IS_COMPLEX
COMPLEX AddScalar(const COMPLEX a)
{
    const COMPLEX b = g_params.s0;
    return COMPLEX(a.real + b.real, a.imag + b.imag);
}
#else
COMPUTE_TYPE AddScalar(const COMPUTE_TYPE x)
{
    return x + COMPUTE_TYPE(g_params.s0);
}
#endif
#endif

#if defined(SubtractScalar)
#if DATA_TYPE_IS_COMPLEX
COMPLEX SubtractScalar(const COMPLEX a)
{
    const COMPLEX b = g_params.s0;
    return COMPLEX(a.real - b.real, a.imag - b.imag);
}
#else
COMPUTE_TYPE SubtractScalar(const COMPUTE_TYPE x)
{
    return x - COMPUTE_TYPE(g_params.s0);
}
#endif
#endif

#if defined(MultiplyScalar)
#if DATA_TYPE_IS_COMPLEX
COMPLEX MultiplyScalar(const COMPLEX a)
{
    const COMPLEX b = g_params.s0;
    return COMPLEX(a.real * b.real - a.imag * b.imag, a.real * b.imag + a.imag * b.real);
}
#else
COMPUTE_TYPE MultiplyScalar(const COMPUTE_TYPE x)
{
    return x * COMPUTE_TYPE(g_params.s0);
}
#endif
#endif

#if defined(DivideScalar)
#if DATA_TYPE_IS_COMPLEX
COMPLEX DivideScalar(const COMPLEX a)
{
    const COMPLEX b = g_params.s0;
    COMPLEX_COMPONENT_TYPE denom = b.real * b.real + b.imag * b.imag;
    return COMPLEX((a.real * b.real + a.imag * b.imag) / denom, (a.imag * b.real - a.real * b.imag) / denom);
}
#else
COMPUTE_TYPE DivideScalar(const COMPUTE_TYPE x)
{
    return x / COMPUTE_TYPE(g_params.s0);
}
#endif
#endif

#if defined(RemainderScalar)
COMPUTE_TYPE RemainderScalar(const COMPUTE_TYPE a)
{
    const COMPUTE_TYPE b = COMPUTE_TYPE(g_params.s0);
#if DATA_TYPE_IS_FLOATING_POINT
    return mod(a, b);
#else
    return COMPUTE_TYPE(int(a) % int(b));
#endif
}
#endif

#if defined(BitwiseAndScalar)
COMPUTE_TYPE BitwiseAndScalar(const COMPUTE_TYPE x)
{
    return x & COMPUTE_TYPE(g_params.s0);
}
#endif

#if defined(BitwiseNot)
COMPUTE_TYPE BitwiseNot(const COMPUTE_TYPE x)
{
    return ~x;
}
#endif

#if defined(BitwiseOrScalar)
COMPUTE_TYPE BitwiseOrScalar(const COMPUTE_TYPE x)
{
    return x | COMPUTE_TYPE(g_params.s0);
}
#endif

#if defined(BitwiseXorScalar)
COMPUTE_TYPE BitwiseXorScalar(const COMPUTE_TYPE x)
{
    return x ^ COMPUTE_TYPE(g_params.s0);
}
#endif

void main()
{
    const uint SizeN = g_sizes[0];
    const uint SizeC = g_sizes[1];
    const uint SizeH = g_sizes[2];
    const uint SizeW = g_sizes[3];
    const uint n = gl_GlobalInvocationID.z;
    const uint h = gl_GlobalInvocationID.y;
    const uint w = gl_GlobalInvocationID.x;
    if ((w >= SizeW) || (h >= SizeH))
    {
        return;
    }
    for (uint c = 0; c < SizeC; ++c)
    {
        uint inputIndex = g_inputIndexOffset + n * g_inputStrides[0] + c * g_inputStrides[1] +
                            h * g_inputStrides[2] + w * g_inputStrides[3];
        uint outputIndex = g_outputIndexOffset + n * g_outputStrides[0] + c * g_outputStrides[1] +
                            h * g_outputStrides[2] + w * g_outputStrides[3];
#if DATA_TYPE_IS_COMPLEX
        const COMPLEX x = g_inputTensor[inputIndex];
        g_outputTensor[outputIndex] = ELEMENT_OP(x);
#else
        const COMPUTE_TYPE x = COMPUTE_TYPE(g_inputTensor[inputIndex]);
        g_outputTensor[outputIndex] = DATA_TYPE(ELEMENT_OP(x));
#endif
    }
}
