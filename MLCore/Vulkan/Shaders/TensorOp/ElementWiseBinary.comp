#version 450 core

#extension GL_GOOGLE_include_directive : require

#include "Common/DataType.h"

layout(push_constant, std430) uniform PushConstants
{
    uint g_inputIndexOffset;
    uint g_otherIndexOffset;
    uint g_outputIndexOffset;
};

#ifndef PARAM_TYPE
    #if DATA_TYPE_IS_FLOATING_POINT
        #if ELEMENT_SIZE <= 4
            #define PARAM_TYPE f32vec4
        #elif DATA_TYPE_ID == DataTypeFloat64
            #define PARAM_TYPE f64vec4
        #endif
    #elif DATA_TYPE_IS_SIGNED_INTEGRAL
        #if ELEMENT_SIZE <= 4
            #define PARAM_TYPE i32vec4
        #else
            #define PARAM_TYPE i64vec4
        #endif
    #elif DATA_TYPE_IS_UNSIGNED_INTEGRAL
        #if ELEMENT_SIZE <= 4
            #define PARAM_TYPE u32vec4
        #else
            #define PARAM_TYPE u64vec4
        #endif
    #elif DATA_TYPE_IS_BOOL
        #define PARAM_TYPE u32vec4
    #elif DATA_TYPE_IS_COMPLEX
        struct Params { DATA_TYPE x; DATA_TYPE y; DATA_TYPE z; DATA_TYPE w; };
        #define PARAM_TYPE Params
    #endif // DATA_TYPE_IS_*
#endif // PARAM_TYPE

layout(set = 0, binding = 0) uniform Uniforms
{
    uvec4 g_sizes;
    uvec4 g_inputStrides;
    uvec4 g_otherStrides;
    uvec4 g_outputStrides;
    PARAM_TYPE g_params;
};

layout(set = 0, binding = 1) readonly buffer InputTensor { DATA_TYPE g_inputTensor[]; };
layout(set = 0, binding = 2) readonly buffer OtherTensor { DATA_TYPE g_otherTensor[]; };
layout(set = 0, binding = 3) buffer OutputTensor { DATA_TYPE g_outputTensor[]; };

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;


#if defined(Add)
#if DATA_TYPE_IS_COMPLEX
COMPLEX Multiply(in COMPLEX a, in COMPLEX b)
{
    return COMPLEX(a.real * b.real - a.imag * b.imag, a.real * b.imag + a.imag * b.real);
}
COMPLEX Add(in COMPLEX a, in COMPLEX b)
{
    b = Multiply(g_params.x, b);
    return COMPLEX(a.real + b.real, a.imag + b.imag);
}
#else
COMPUTE_TYPE Add(COMPUTE_TYPE a, COMPUTE_TYPE b)
{
    return a + COMPUTE_TYPE(g_params[0]) * b;
}
#endif
#endif

#if defined(Subtract)
#if DATA_TYPE_IS_COMPLEX
COMPLEX Subtract(in COMPLEX a, in COMPLEX b)
{
    return COMPLEX(a.real - b.real, a.imag - b.imag);
}
#else
COMPUTE_TYPE Subtract(COMPUTE_TYPE a, COMPUTE_TYPE b)
{
    return a - COMPUTE_TYPE(g_params[0]) * b;
}
#endif
#endif

#if defined(Multiply)
#if DATA_TYPE_IS_COMPLEX
COMPLEX Multiply(in COMPLEX a, in COMPLEX b)
{
    return COMPLEX(a.real * b.real - a.imag * b.imag, a.real * b.imag + a.imag * b.real);
}
#else
COMPUTE_TYPE Multiply(COMPUTE_TYPE a, COMPUTE_TYPE b)
{
    return a * b;
}
#endif
#endif

#if defined(Divide)
#if DATA_TYPE_IS_COMPLEX
COMPLEX Divide(in COMPLEX a, in COMPLEX b)
{
    COMPONENT_TYPE denom = b.real * b.real + b.imag * b.imag;
    return COMPLEX((a.real * b.real + a.imag * b.imag) / denom, (a.imag * b.real - a.real * b.imag) / denom);
}
#else
COMPUTE_TYPE Divide(COMPUTE_TYPE a, COMPUTE_TYPE b)
{
    return a / b;
}
#endif
#endif

#if defined(Remainder)
COMPUTE_TYPE Remainder(COMPUTE_TYPE a, COMPUTE_TYPE b)
{
#if DATA_TYPE_IS_FLOATING_POINT
    return mod(a, b);
#else
    return a % b;
#endif
}
#endif

#if defined(BitwiseAnd)
COMPUTE_TYPE BitwiseAnd(COMPUTE_TYPE a, COMPUTE_TYPE b)
{
    return a & b;
}
#endif

#if defined(BitwiseOr)
COMPUTE_TYPE BitwiseOr(COMPUTE_TYPE a, COMPUTE_TYPE b)
{
    return a | b;
}
#endif

#if defined(BitwiseXor)
COMPUTE_TYPE BitwiseXor(COMPUTE_TYPE a, COMPUTE_TYPE b)
{
    return a ^ b;
}
#endif

void main()
{
    const uint SizeN = g_sizes[0];
    const uint SizeC = g_sizes[1];
    const uint SizeH = g_sizes[2];
    const uint SizeW = g_sizes[3];
    const uint n = gl_GlobalInvocationID.z;
    const uint h = gl_GlobalInvocationID.y;
    const uint w = gl_GlobalInvocationID.x;
    if ((w >= SizeW) || (h >= SizeH))
    {
        return;
    }
    for (uint c = 0; c < SizeC; ++c)
    {
        uint inputIndex = g_inputIndexOffset + n * g_inputStrides[0] + c * g_inputStrides[1] +
                            h * g_inputStrides[2] + w * g_inputStrides[3];
        uint otherIndex = g_otherIndexOffset + n * g_otherStrides[0] + c * g_otherStrides[1] +
                            h * g_otherStrides[2] + w * g_otherStrides[3];
        uint outputIndex = g_outputIndexOffset + n * g_outputStrides[0] + c * g_outputStrides[1] +
                            h * g_outputStrides[2] + w * g_outputStrides[3];
#if DATA_TYPE_IS_COMPLEX
        const COMPLEX a = g_inputTensor[inputIndex];
        const COMPLEX b = g_otherTensor[otherIndex];
        g_outputTensor[outputIndex] = ElementOp(a, b);
#else
        const COMPUTE_TYPE a = COMPUTE_TYPE(g_inputTensor[inputIndex]);
        const COMPUTE_TYPE b = COMPUTE_TYPE(g_otherTensor[otherIndex]);
        g_outputTensor[outputIndex] = DATA_TYPE(ElementOp(a, b));
#endif
    }
}
