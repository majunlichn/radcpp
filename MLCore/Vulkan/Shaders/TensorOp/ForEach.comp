#version 450 core

#extension GL_GOOGLE_include_directive : require

#include "Common/DataType.h"

layout(push_constant, std430) uniform PushConstants
{
    uint g_inputIndexOffset;
};

#ifndef PARAM_TYPE
    #if DATA_TYPE_IS_FLOATING_POINT
        #if ELEMENT_SIZE <= 4
            #define PARAM_TYPE f32vec4
        #else
            #define PARAM_TYPE f64vec4
        #endif
    #elif DATA_TYPE_IS_SIGNED_INTEGRAL
        #if ELEMENT_SIZE <= 4
            #define PARAM_TYPE i32vec4
        #else
            #define PARAM_TYPE i64vec4
        #endif
    #elif DATA_TYPE_IS_UNSIGNED_INTEGRAL
        #if ELEMENT_SIZE <= 4
            #define PARAM_TYPE u32vec4
        #else
            #define PARAM_TYPE u64vec4
        #endif
    #endif // DATA_TYPE_IS_*
#endif // PARAM_TYPE

layout(set = 0, binding = 0) uniform Uniforms
{
    uvec4 g_sizes;
    uvec4 g_strides;
    PARAM_TYPE g_params;
};

layout(set = 0, binding = 1) buffer InputTensor { DATA_TYPE g_inputTensor[]; };

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

#if defined(Fill)
DATA_TYPE Fill(uint n, uint c, uint h, uint w)
{
    return DATA_TYPE(g_params[0]);
}
#endif

void main()
{
    const uint SizeN = g_sizes[0];
    const uint SizeC = g_sizes[1];
    const uint SizeH = g_sizes[2];
    const uint SizeW = g_sizes[3];
    const uint n = gl_GlobalInvocationID.z;
    const uint h = gl_GlobalInvocationID.y;
    const uint w = gl_GlobalInvocationID.x;
    if ((w >= SizeW) || (h >= SizeH))
    {
        return;
    }
    for (uint c = 0; c < SizeC; ++c)
    {
        uint bufferIndex = g_inputIndexOffset + n * g_strides[0] + c * g_strides[1] +
                            h * g_strides[2] + w * g_strides[3];
        g_inputTensor[bufferIndex] = ElementOp(n, c, h, w);
    }
}
