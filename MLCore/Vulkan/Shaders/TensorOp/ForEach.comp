#version 450 core

#extension GL_GOOGLE_include_directive : require

#include "Common/DataType.h"

layout(push_constant, std430) uniform PushConstants
{
    uint g_inputIndexOffset;
};

#if ((DATA_TYPE_ID == DataTypeFloat16) || (DATA_TYPE_ID == DataTypeFloat32) ||\
    (DATA_TYPE_ID == DataTypeBFloat16) || (DATA_TYPE_ID == DataTypeFloat8E4M3) || (DATA_TYPE_ID == DataTypeFloat8E5M2))
    struct Params { float32_t s0; float32_t s1; float32_t s2; float32_t s3; };
#elif (DATA_TYPE_ID == DataTypeFloat64)
    struct Params { float64_t s0; float64_t s1; float64_t s2; float64_t s3; };
#elif ((DATA_TYPE_ID == DataTypeSint8) || (DATA_TYPE_ID == DataTypeSint16) || (DATA_TYPE_ID == DataTypeSint32))
    struct Params { int32_t s0; int32_t s1; int32_t s2; int32_t s3; };
#elif (DATA_TYPE_ID == DataTypeSint64)
    struct Params { int64_t s0; int64_t s1; int64_t s2; int64_t s3; };
#elif ((DATA_TYPE_ID == DataTypeUint8) || (DATA_TYPE_ID == DataTypeUint16) || (DATA_TYPE_ID == DataTypeUint32))
    struct Params { uint32_t s0; uint32_t s1; uint32_t s2; uint32_t s3; };
#elif (DATA_TYPE_ID == DataTypeUint64)
    struct Params { uint64_t s0; uint64_t s1; uint64_t s2; uint64_t s3; };
#elif (DATA_TYPE_ID == DataTypeBool)
    struct Params { uint32_t s0; uint32_t s1; uint32_t s2; uint32_t s3; };
#elif DATA_TYPE_IS_COMPLEX
    struct Params { COMPLEX s0; COMPLEX s1; COMPLEX s2; COMPLEX s3; };
#endif

layout(set = 0, binding = 0) uniform Uniforms
{
    uvec4 g_sizes;
    uvec4 g_strides;
    Params g_params;
};

layout(set = 0, binding = 1) buffer InputTensor { DATA_TYPE g_inputTensor[]; };

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

#if defined(Fill)
void Fill(uint n, uint c, uint h, uint w, uint bufferIndex)
{
#if DATA_TYPE_IS_COMPLEX
    g_inputTensor[bufferIndex] = g_params.s0;
#else
    g_inputTensor[bufferIndex] = DATA_TYPE(g_params.s0);
#endif
}
#endif

void main()
{
    const uint SizeN = g_sizes[0];
    const uint SizeC = g_sizes[1];
    const uint SizeH = g_sizes[2];
    const uint SizeW = g_sizes[3];
    const uint n = gl_GlobalInvocationID.z;
    const uint h = gl_GlobalInvocationID.y;
    const uint w = gl_GlobalInvocationID.x;
    if ((w >= SizeW) || (h >= SizeH))
    {
        return;
    }
    for (uint c = 0; c < SizeC; ++c)
    {
        uint bufferIndex = g_inputIndexOffset + n * g_strides[0] + c * g_strides[1] +
                            h * g_strides[2] + w * g_strides[3];
        ELEMENT_OP(n, c, h, w, bufferIndex);
    }
}
